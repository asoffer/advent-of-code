--     ::= import "core.ic"
cstdio ::= import "cstdio.ic"
io     ::= import "io.ic"
file   ::= import "file.ic"

NUM_LINES ::= 1000
nums: [NUM_LINES; int64]

file.File ("input.txt") open [f: *cstdio.File] {
  fscanf ::= foreign("fscanf", (*cstdio.File, [*]nat8, *int64) -> int32)
  index := 0
  while (fscanf(f, &"%lld"[0], &nums[index]) != cstdio.eof) do { index += 1 }
}

validate ::= (ptr: [*]int64, len: int64) -> int64 {
  last := ptr[len - 1]
  for (0, len - 2) do [i: int64] {
    for (i, len - 1) do [j: int64] {
      if (ptr[i] + ptr[j] == last) then { return -1 }
    }
  }
  return last
}

sum ::= (ptr: [*]int64, len: int64) -> int64 {
  total := 0
  for (0, len) do [i: int64] { total += ptr[i] }
  return total
}

max ::= (ptr: [*]int64, len: int64) -> int64 {
  m := 0
  for (0, len) do [i: int64] {
    if (m < ptr[i]) then { m = ptr[i] }
  }
  return m
}

min ::= (ptr: [*]int64, len: int64) -> int64 {
  m := 1024 * 1024 * 1024 * 1024 * 1024 * 1024
  for (0, len) do [i: int64] {
    if (m > ptr[i]) then { m = ptr[i] }
  }
  return m
}

magic_num ::= (nums: [*]int64) -> int64 {
  for (0, NUM_LINES - 25) do [i: int64] {
    num := validate(&nums[i], 26)
    if (num != -1) then {
      return num
    }
  }
}

num := (&nums[0])'magic_num

for (0, NUM_LINES - 25) do [i: int64] {
  for (1, 25) do [j: int64] {
    s := sum(&nums[i], j)
    if (num == s) then {
      io.Print(min(&nums[i], j) + max(&nums[i], j))
      io.Print("\n")
    }
  }
}
