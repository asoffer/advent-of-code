--     ::= import "core.ic"
cstdio ::= import "cstdio.ic"
io     ::= import "io.ic"
file   ::= import "file.ic"

fscanf ::= foreign("fscanf",
  (*cstdio.File, [*]char, *i64, *i64, *char, [*]char) -> i32)

valid_password ::= (password: [*]char, character: char,
                    first: i64, second: i64) -> bool {
  return (password[first - 1] == character) xor (password[second - 1] == character)
}

success_count := 0

file.File ("input.txt") open [f: *cstdio.File] {
  first: i64
  second: i64
  character: char
  password: [1024; char]

  while (fscanf(f, "%lld-%lld %c: %s".data,
         &first, &second, &character, &password[0]) != cstdio.eof) do {
    if (valid_password(&password[0], character, first, second)) then {
      success_count += 1
    }
  }
}

io.Print(success_count)
